" Author: fcrh
" Email: coquelicot1117@gmail.com

" Vundle {{{ -----------------------------------------------------------------

" Start
filetype off
set nocompatible
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

" Plugins
Plugin 'VundleVim/Vundle.vim'
Plugin 'scrooloose/nerdtree'
Plugin 'vim-syntastic/syntastic'
Plugin 'majutsushi/tagbar'
Plugin 'tpope/vim-surround'
Plugin 'ctrlpvim/ctrlp.vim'
Plugin 'ap/vim-buftabline'

" End
call vundle#end()
filetype plugin indent on

" }}}

" Basic setting {{{ ----------------------------------------------------------

syntax on
colorscheme elflord

set exrc
set modeline
set modelines=5

set number
set ruler
set showcmd
set cursorline

set backspace=2

set expandtab
set shiftwidth=4
set tabstop=4
set softtabstop=4

set scrolloff=5

set hlsearch
set incsearch
set ignorecase
set smartcase

set autoindent
set copyindent

set foldmarker={{{,}}}
set foldmethod=marker

set mouse=a

set wildmenu
set wildignore=*.swp,*un~,*.o,*.pyc,*.class

set title

set hidden
set confirm

set backup
set undofile
set backupcopy=yes

set lazyredraw

" For Unix and Win32, if a directory ends in two path separators "//" or "\\", 
" the swap file name will be built from the complete path to the file with all
" path separators substituted to percent '%' signs. This will ensure file name
" uniqueness in the preserve directory.
set backupdir=~/.vim/backup//
set undodir=~/.vim/undo//
set dir=~/.vim/swp//

" }}}

" Keycodes {{{ ---------------------------------------------------------------

" remap leader
let mapleader=" "

" shortten the timeout of keycode
set ttimeoutlen=150

" bracketed paste prefix
execute "set <f13>=\<ESC>[200~"
" bracketed paste suffix
execute "set <f14>=\<ESC>[201~"

" }}}

" Mapping {{{ ----------------------------------------------------------------

nnoremap <LEADER>s :shell<CR>
nnoremap <LEADER>p :!python<CR>
nnoremap <LEADER>r :!./%<CR>

" NERDTree required
nnoremap <LEADER>f :NERDTreeFocus<CR>

" Tagbar required
nnoremap <LEADER>t :TagbarToggle<CR>

" CtrlP required
nnoremap <C-b> :CtrlPMRU<CR>

" get rid of highlight search
nnoremap <LEADER><LEADER> :nohlsearch<CR>

" reselect
vnoremap > >gv
vnoremap < <gv

nnoremap ; :

" move to next/previous row instead of line
nnoremap j gj
nnoremap k gk

" buffer helper
nnoremap <C-l> :bnext<CR>
nnoremap <C-h> :bprev<CR>
nnoremap <C-d> :bd<CR>

" clear screen
nnoremap <LEADER>l :redraw!<CR>

" Ctrl-C
nnoremap <C-c> :q<CR>

" https://github.com/peter50216/dotfiles/blob/master/config/vimrc
inoremap jk <ESC>
nnoremap ^ 0
nnoremap 0 ^
vnoremap ^ 0
vnoremap 0 ^

" }}}

" Paste {{{ ------------------------------------------------------------------
" http://stackoverflow.com/questions/5585129

if &term =~ "xterm.*" || exists("$TMUX")

    " enable/disable bracketed paste mode
    let &t_ti = &t_ti . "\e[?2004h"
    let &t_te = "\e[?2004l" . &t_te

    function! XTermPasteBegin(ret)
        " stop paste mode when bracketed end
        set pastetoggle=<f14>
        " start paste mode
        set paste
        " extra command code to which it'll be mapped
        return a:ret
    endfunction

    map <expr> <f13> XTermPasteBegin("i")
    imap <expr> <f13> XTermPasteBegin("")
    vmap <expr> <f13> XTermPasteBegin("c")

    " do nothing when we're in command mode
    cmap <f13> <nop>
    cmap <f14> <nop>

endif

" }}}

" Specialization {{{ ---------------------------------------------------------

" c,cpp
autocmd FileType c,cpp nnoremap \r :!./%<<CR>
autocmd Filetype c,cpp set cin
autocmd Filetype c,cpp set makeprg=g++\ -O2\ -std=c++11\ %\ -o\ %<

" makefile
autocmd Filetype make set noexpandtab

" html,css,js
autocmd Filetype htmldjango,html,css,javascript set shiftwidth=2
autocmd Filetype htmldjango,html,css,javascript set tabstop=2
autocmd Filetype htmldjango,html,css,javascript set softtabstop=2

" }}}

" Setup backup/undo dir  {{{ -------------------------------------------------

function CreateDirs(dirs)
    for dir in split(a:dirs, ',')
        let cleaned_dir = substitute(dir, '/*$', '', '')
        if isdirectory(cleaned_dir) == 0
            echo "Creating dir: " . cleaned_dir
            call mkdir(cleaned_dir, 'p')
        endif
    endfor
endfunction

call CreateDirs(&backupdir)
call CreateDirs(&undodir)
call CreateDirs(&directory)

" }}}

" Syntastic required {{{ -----------------------------------------------------

set statusline+=%#warningmsg#
set statusline+=%{SyntasticStatuslineFlag()}
set statusline+=%*

let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0

" eslint required
let g:syntastic_javascript_checkers = ['eslint']

" }}}

" Buftabline required {{{ ----------------------------------------------------

let g:buftabline_indicators = 1

" }}}

" Load local config {{{ ------------------------------------------------------

function ReadRCOnPath(target_path)
    let current_path = ''
    for dir in split(a:target_path, '/')
        let current_path = current_path . '/' . dir
        let file = current_path . '/' . '.vimrc.local'
        if filereadable(file) &&
                \ empty(matchstr(getfperm(file), '^r.-------$')) == 0
            execute 'source ' . file
        endif
    endfor
endfunction
call ReadRCOnPath(getcwd())

" }}}

" Misc {{{ -------------------------------------------------------------------

" In vim env
let $IN_VIM = 'yes'

" }}}
